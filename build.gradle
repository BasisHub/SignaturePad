apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'net.seabears'
version = '1.0.0'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    // api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    // implementation 'com.google.guava:guava:22.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.0.0'
    testImplementation 'commons-io:commons-io:2.5'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'john-hancock'
                packaging 'jar'
                description 'Build images from common customer-signature data serialization formats.'
                url 'https://github.com/cberes/john-hancock'

                scm {
                    url 'https://github.com/cberes/john-hancock.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://mit-license.org/license.txt'
                    }
                }

                developers {
                    developer {
                        id 'cberes'
                        name 'Corey Beres'
                        email 'corey.beres@gmail.com'
                    }
                }
            }
        }
    }
}
